// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:
   // zero the x/y input when sel = 1:
   Mux16(a=x, b=false, sel=zx, out=zxout);
   Mux16(a=y, b=false, sel=zy, out=zyout);

   // negate the x/y input when sel = 1:
   Not16(in=zxout, out=Notx);
   Not16(in=zyout, out=Noty);
   Mux16(a=zxout, b=Notx, sel=nx, out=zxout2);
   Mux16(a=zyout, b=Noty, sel=ny, out=zyout2);   

   // integer 2's complement addition:
   Add16(a=zxout2, b=zyout2, out=addout);
   // bitwise and:
   And16(a=zxout2, b=zyout2, out=andout);

   Mux16(a=andout, b=addout, sel=f, out=fout);  

   // bitwise not:
   Not16(in=fout, out=Notfout);
   // if (out < 0) set ng = 1
   // check if out[0..15] == 0
   Mux16(a=fout, b=Notfout, sel=no, out=out, out[0..7]=by1, out[8..15]=by2, out[15] = ng);  

   // if (out == 0) set zr = 1:
   // now check if zr (all bits 0): if all bits 0, then output 0 using Or8Way
   Or8Way(in=by1, out=pos1);
   Or8Way(in=by2, out=pos2);
   Or(a=pos1, b=pos2, out=pos);
   Not(in=pos, out=zr);
   




   

















}