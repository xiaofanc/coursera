/**
 * Implementation of the Mario game
 */

class MarioGame {
    
    // the running mario that player controls
    field Mario mario;

    // box is the blocks that mario can hit to get score
    field Array boxes;
    field int numBox;
    field Box box;  // reference used in loop


    // score: keep track of current score
    // i: index for boxes
    field int score, i;

    constructor MarioGame new() {
        // initialize and draw mario
        let mario = Mario.new(200-32);
        do mario.draw();

        // initialize array of boxes and draw them
        let numBox = 2;
        let boxes = Array.new(numBox);
        let boxes[0] = Box.new(506, 48, 30);
        let boxes[1] = Box.new(606, 48, 25);
        let i = 0;
        while (i<numBox) {
            let box = boxes[i];
            do box.draw();
            let i = i+1;
        }
        
        // draw ground, a horizontal line
        do drawGround();
        
        return this;
    }

    method void dispose() {
        do mario.dispose();
        let i = 0;
        while (i<numBox) {
            // dispose all boxes
            let box = boxes[i];
            do box.dispose();
            let i = i+1;
        }
        do Memory.deAlloc(this);
        return;
    }

    method void drawGround() {
        do Screen.drawLine(0+3, 200+1, 511-3, 200+1);
        return;
    }

    method void run() {
        var char key;       // the key currently pressed by the user
        var int boxX;    // box's x coordinate
        var boolean exit;
        let exit = false;

        do Output.moveCursor(0,0);
        do Output.printString("[SPACE]Jump, [Q]Quit");

        while (~exit) {
            let key = Keyboard.keyPressed();
            do mario.move();
            let i = 0;
            while (i<numBox) {
                let box = boxes[i];
                do box.move();
                let boxX = box.getX();
                if (boxX < 32) {
                    // mario hit box, then score ++
                    if (mario.getBottomY() + Math.abs(boxX-12) + 3 < box.getWidth()) {
                        let score = score + 1;

                        do Output.moveCursor(0, 50);
                        do Output.printString("Score: ");
                        do Output.printInt(score);
                    }

                    // move box to the right of screen (reset X), set random width (20-40)
                    if (boxX < 10) {
                        do box.clear();
                        do box.setX(511+Random.randRange(100));
                        do box.setW(20+Random.randRange(10));
                    }

                }
                let i = i+1;
            }
            if (key = 32) { // space key
                do mario.jump();
            }
            if (key = 81) { // q key
                let exit = true;
            }
            do Sys.wait(50);
        } // while
        return;
    }
}